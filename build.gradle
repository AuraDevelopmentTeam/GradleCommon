import org.apache.tools.ant.DirectoryScanner
import org.apache.tools.ant.taskdefs.condition.Os

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.ajoberstar:grgit:2.0.0"
    }
}

description "Common Gradle Build Script"

def getLatestTag() {
    return "git rev-list --tags --max-count=1".execute(null, projectDir).text.trim()
}

if (project.hasProperty("newProjectDir")) {
    File destiontionDir = new File(newProjectDir)

    task initRepo(type: Exec) {
        workingDir destiontionDir

        executable "git"
        args "init"
    }

    task addSubmodule(type: Exec, dependsOn: initRepo) {
        workingDir destiontionDir

        executable "git"
        args "submodule", "add", "https://github.com/AuraDevelopmentTeam/GradleCommon.git"

        ignoreExitValue = true
    }

    task setGitTag(type: Exec, dependsOn: addSubmodule) {
        workingDir new File(destiontionDir, "GradleCommon")

        executable "git"
        args "checkout", "${-> getLatestTag()}"
    }

    task initGradle(type: Exec) {
        workingDir destiontionDir

        executable file(Os.isFamily(Os.FAMILY_WINDOWS)? "gradlew.bat" : "gradlew")
        args "-Dorg.gradle.daemon=false", "init", "wrapper"

        shouldRunAfter setGitTag
    }

    task copyTemplateFiles(type: Copy, dependsOn: initGradle) {
        doFirst {
            DirectoryScanner.defaultExcludes.each { DirectoryScanner.removeDefaultExclude it }
            DirectoryScanner.addDefaultExclude "something has to be in here or everything gets excluded"
        }

        from (projectDir) {
            include ".gitattributes"
            include ".gitignore"
        }

        from "template"

        into destiontionDir

        doLast {
            DirectoryScanner.resetDefaultExcludes()
        }
    }

    task addAllFiles(type: Exec, dependsOn: [setGitTag, copyTemplateFiles]) {
        workingDir destiontionDir

        executable "git"
        args "add", "-A"
    }

    task makeGradlewExecutable(type: Exec, dependsOn: addAllFiles) {
        workingDir destiontionDir

        executable "git"
        args "update-index", "--chmod=+x", "gradlew"
    }

    task createFirstCommit(type: Exec, dependsOn: makeGradlewExecutable) {
        workingDir destiontionDir

        executable "git"
        args "commit", "-m", "Initial commit!\n\n[ci skip]"
    }

    task createFirstTag(type: Exec, dependsOn: createFirstCommit) {
        workingDir destiontionDir

        executable "git"
        args "tag", "-a", "v0.0.0", "-m", "Project starts!"
    }

    task addGitHooks(type: Exec, dependsOn: initGradle) {
        workingDir destiontionDir

        executable file(Os.isFamily(Os.FAMILY_WINDOWS)? "gradlew.bat" : "gradlew")
        args "-Dorg.gradle.daemon=false", "addGitHooks"

        shouldRunAfter createFirstTag
    }

    task createNewProject(dependsOn: [initRepo, addSubmodule, setGitTag, initGradle, copyTemplateFiles, addAllFiles, makeGradlewExecutable, createFirstCommit, createFirstTag, addGitHooks]) {}
}

task resetChanges(type: Exec) {
    workingDir projectDir

    executable "git"
    args "reset", "--hard"
}

task fetchUpdates(type: Exec, dependsOn: resetChanges) {
    workingDir projectDir

    executable "git"
    args "fetch", "--tags"
}

task checkoutLatestTag(type: Exec, dependsOn: fetchUpdates) {
    workingDir projectDir

    executable "git"
    args "checkout", "${-> getLatestTag()}"
}

task updateTemplateFiles(dependsOn: checkoutLatestTag) {
    doLast {
        DirectoryScanner.defaultExcludes.each { DirectoryScanner.removeDefaultExclude it }
        DirectoryScanner.addDefaultExclude "something has to be in here or everything gets excluded"

        copy {
            from (project.file("template")) {
                include ".gitlab-ci.yml"
            }

            from (project.file(".")) {
                include ".gitignore"
                include ".gitattributes"
            }

            into project.file("..")
        }

        DirectoryScanner.resetDefaultExcludes()
    }
}

task stageUpdate(type: Exec, dependsOn: updateTemplateFiles) {
    workingDir project.file("..")

    executable "git"
    args "add", "GradleCommon", ".gitlab-ci.yml", ".gitignore", ".gitattributes"
}

task commitUpdate(type: Exec, dependsOn: stageUpdate) {
    workingDir project.file("..")

    executable "git"
    args "commit", "-m", "Updated Gradle Common"

    ignoreExitValue = true
}

task updateGradleCommon(dependsOn: [resetChanges, fetchUpdates, checkoutLatestTag, updateTemplateFiles, stageUpdate, commitUpdate])
